plugins {
    id 'java'
    alias spigots.plugins.spigot
    alias commons.plugins.ideaExt
}

repositories {
    mavenCentral()
    spigotRepos {
        papermc()
        jitpack()
    }
}

dependencies {
    compileOnly spigots.paper.api
    compileOnly spigots.protocolLib
    compileOnly(spigots.vault.api) {
        transitive = false // No want to import vault's internal dependencies.
    }
    testImplementation(platform(commons.junit.bom))
    testImplementation(commons.junit.jupiter)
    testRuntimeOnly(commons.junit.platform.launcher)
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(21))
    }
}

spigot {
    description = 'A sample plugin'
    depend = ['ProtocolLib', 'Vault']
    apiVersion = '1.21'
    load = "STARTUP"
    commands {
        register('give') {
            aliases = ['giv', 'i']
            description = 'A give command.'
            permission = 'sample.give'
            permissionMessage = 'You do not have the permission.'
        }
    }
    permissions {
        register('sample.give') {
            description = 'Allows give command'
            defaults = 'true'
        }
        register('sample.*') {
            description = 'Wildcard permission'
            defaults = 'op'
            children = ['test.foo': true]
        }
    }
}

debugSpigot {
    version.set('1.21.8')
    eula.set(true)
}
