plugins {
    id 'java'
    id 'io.typst.spigradle' version '3.1.2'
}

group 'kr.entree'
version '1.0-SNAPSHOT'

compileJava.options.encoding 'UTF-8'

repositories {
    mavenCentral()
    spigotmc()
    jitpack() // For vault
}

dependencies {
    compileOnly spigot()
    compileOnly protocolLib()
    compileOnly(vault()) {
        transitive = false // No want to import vault's internal dependencies.
    }
    testImplementation group: 'junit', name: 'junit', version: '4.12'
}

import io.typst.spigradle.spigot.*

spigot {
    description 'A sample plugin'
    depends 'ProtocolLib', 'Vault'
    load Load.STARTUP // or implicitly "load STARTUP".
    commands {
        give {
            aliases 'giv', 'i'
            description 'A give command.'
            permission 'sample.give'
            permissionMessage 'You do not have the permission.'
        }
    }
    permissions {
        'sample.give' {
            description 'Allows give command'
            defaults 'true'
        }
        'sample.*' {
            description 'Wildcard permission'
            defaults 'op'
            children = ['test.foo': true]
        }
    }
}
